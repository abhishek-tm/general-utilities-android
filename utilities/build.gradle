apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.android.gms:play-services-plus:10.0.0'
    compile 'com.google.android.gms:play-services-maps:10.0.0'
    compile 'com.google.android.gms:play-services-analytics:10.0.0'
    compile 'com.google.android.gms:play-services-location:10.0.0'
}

// tasks for packaging source and javadocs artifacts

task javadocGenerate(type: Javadoc) {
    // prints javadoc outputs when info logging level set
    logging.captureStandardError LogLevel.INFO

    description = "Generates javadoc"
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocPackage(dependsOn: javadocGenerate, type: Jar) {
    baseName = "utilities"
    classifier = "javadoc"
    version "1.0.5"
    from tasks.javadocGenerate.destinationDir
}

task sourcePackage(type: Jar) {
    baseName = "utilities"
    classifier = "sources"
    version "1.0.5"
    from android.sourceSets.main.java.srcDirs
}

//configure sign plugin

artifacts {
    archives javadocPackage, sourcePackage
}